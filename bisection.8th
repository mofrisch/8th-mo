needs numerics
needs tools

var f(a)

: f-test \ n -- n
   \ f(x)=x^3+4x^2-10
   dup >r 4 n:+ r@ n:* r> n:* 10 n:- ;

: midpoint \ a b -- a+(b-a)/2
   over n:- 2 n:/ n:+ ; 

: print-row \ n a b p f(p) -- n a b p f(p)
   4 #> 4 pick . " " . 
   3 pick f. " " .
   2 pick f. " " .
   1 pick f. " " .
   0 pick f. " " .
   3 pick 2 pick n:- n:abs f. 
   cr ;

: criterion \ n b p f(p) -- n b p f(p) ?
   dup 0 eps f~ >r 
   -rot 2dup eps f~ >r rot 
   2r> or ;

: crit \ n a b -- n a b ?
   f(a) @ 0 eps f~ >r \ n a b
   2dup eps f~ r> \ n ? a b ?
   or ;

: next-interval \ a b n --  a p | p b
   dup 11 = if ;; then
   -rot 2dup midpoint dup f \ n a b p f(p)
   num-debug @ if print-row then
   f(a) @ over n:* 0 n:> if \ n a b p f(p) f(a)*f(p)>0
      f(a) ! rot drop swap \ n a b p 
   else 
      drop nip \ n a b 
   then 
   crit if rot break else
   rot drop then ; 

: bisection \ a b -- p
   over f f(a) ! 
   num-debug @ if
   "n     a                       b                       p                       f(p)                    |a-b| " . cr
   124 draw-line cr
   then
   ' next-interval 1 11 loop 11 n:= if
   "ran " . 10 . " iterations, but result was not close enough" . cr then ;

true num-debug !
short-floats

\ 40 big-floats
1e-15 eps!
' f-test w:is f
1. 2. bisection f. cr



